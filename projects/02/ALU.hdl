// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // apply stat to x
    Mux16(a=x[0..15], b[0..15]=false, sel=zx, out=za);
    Not16(in=za, out=nza);
    Mux16(a=za, b=nza, sel=nx, out=statx);

    // apply stat to y
    Mux16(a=y[0..15], b[0..15]=false, sel=zy, out=zb);
    Not16(in=zb, out=nzb);
    Mux16(a=zb, b=nzb, sel=ny, out=staty);

    // Add or And Multiplexer
    Add16(a=statx, b=staty, out=addo);
    And16(a=statx, b=staty, out=ando);
    Mux16(a=ando, b=addo, sel=f, out=tmpout);

    // apply no stat to tmpout
    Not16(in=tmpout, out=ntmpout);
    Mux16(a=tmpout, b=ntmpout, sel=no, out=out, out[0..7]=th, out[8..15]=dh,
    out[15]=msb);

    // set zr and ng flags
    Or8Way(in=th, out=oro1);
    Or8Way(in=dh, out=oro2);
    Or(a=oro1, b=oro2, out=orout);
    Not(in=orout, out=zr);
    And(a=msb, b=true, out=ng);
}
